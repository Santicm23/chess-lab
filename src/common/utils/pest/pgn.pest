WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = { "{" ~ (!"}" ~ ANY)* ~ "}" }

word = { (!("]" | "\"") ~ ANY)+ }
metadata_block = { "[" ~ word ~ "\"" ~ word? ~ "\"]" }
file = { 'a'..'h' }
rank = { '1'..'8' }
piece = { ("K" | "Q" | "R" | "B" | "N") }
square = @{ file ~ rank }
option_square = @{ file? ~ rank? }
castle = { ("O-O-O" | "O-O") }
check_state = { ("+" | "#") }
promotion = @{ "=" ~ (!"K" ~ piece) }
basic_move = ${ piece? ~ ((option_square ~ square) | square) }
capture = ${ (square | file | rank | (piece ~ option_square)) ~ "x" ~ square }
move = ${ (((capture | basic_move) ~ promotion?) | castle) ~ check_state? }
move_number = @{ ('1'..'9') ~ ('0'..'9')* ~ "." }
second_move_number = @{ move_number ~ ".." }
partial_move = { move_number ~ move }
full_move = { partial_move ~ move }
half_move = { second_move_number ~ move }
line = { (full_move)* ~ partial_move? }
sequence = {
	(full_move+ ~ ("(" ~ half_move ~ sequence ~ ")")+ ~ sequence)
	| (full_move* ~ partial_move) ~ ("(" ~ sequence ~ ")")+ ~ (half_move ~ sequence)?
    | line
}
result = { "1/2-1/2" | "1-0" | "0-1" }

pgn = { SOI ~ metadata_block* ~ sequence ~ result? ~ EOI}
